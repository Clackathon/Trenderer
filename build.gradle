plugins {
	id 'maven'
	id 'java'
	id 'idea'
	id 'application'
}

defaultTasks 'build'
group = "com.clackathon"
version = "${buildVersion}"

repositories {
	jcenter()
}

configurations {
	hidden
	compile.extendsFrom hidden
}

dependencies {
	testCompile 'junit:junit:4.12'
	compile 'com.sachinhandiekar:jInstagram:1.1.6'
	compile 'com.google.api-client:google-api-client-assembly:1.21.0'
	compile 'com.google.apis:google-api-services-vision:v1-rev6-1.21.0'
	compile 'com.google.truth:truth:0.28'
	hidden 'org.jetbrains:annotations:15.0'
	hidden 'org.projectlombok:lombok:1.16.4'
}

test {
	environment 'GOOGLE_APPLICATION_CREDENTIALS', file('./secrets/CloudVisionCredentials.json').absoluteFile
}

tasks.withType(JavaCompile) {
	sourceCompatibility = 8
	targetCompatibility = 8
	options.with {
		encoding = 'UTF-8'
		compilerArgs <<
			"-Xlint:all" <<
			"-Xlint:-path" <<
			"-Xlint:-rawtypes" <<
			"-Xlint:-processing"
	}
}

jar {
	baseName = project.name

	manifest.mainAttributes(
		"Built-By": System.properties['user.name'],
		"Created-By": System.properties['java.vm.version'] + " (" + System.properties['java.vm.vendor'] + ")",
		"Implementation-Title": name,
		"Implementation-Version": version
	)
}

task sourcesJar(type: Jar, dependsOn: classes) {
	classifier = 'sources'
	from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
	classifier = 'javadoc'
	from javadoc.destinationDir
}

artifacts {
	archives sourcesJar
	archives javadocJar
	archives jar
}
